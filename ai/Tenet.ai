globals
    constant integer TENET_PIONEER            = 'h005'
    constant integer TENET_FACTORY            = 'h001'
    constant integer TENET_BARRACKS           = 'h00K'
    constant integer TENET_CONTAINER          = 'h004'
	
	constant integer RUINED_BUILDING_1        = 'o000'
	constant integer RUINED_BUILDING_2        = 'o001'
	
	constant integer MACHINE_GUN_TURRET       = 'h00G'
	constant integer FLAMETHROWER_TURRET      = 'h00F'
	constant integer ANTI_AIR_TURRET          = 'h00E'
	
	constant integer UPG_MINE_DETECTOR        = 'R000'
	constant integer UPG_MEDIC                = 'R001'
	constant integer UPG_ENGINEER             = 'R002'
	constant integer UPG_MINES                = 'R005'
	constant integer UPG_ROCKET_LAUNCHER      = 'R003'
	constant integer UPG_STINGER              = 'R004'
	constant integer UPG_IMPROVED_DEFENCE     = 'R007'
	
    constant integer TENET_SOLDIER            = 'z000'
    constant integer HERO_NEIL                = 'H00C'
	
	constant integer SKILLED_DRIVER           = 'A018'
	constant integer SACRIFICE                = 'A00P'
endglobals

function ChooseHeroSkill takes nothing returns integer
    local integer curHero = GetHeroId()
    local integer level = GetHeroLevelAI()

    if (curHero == HERO_NEIL) then
        //if (level == 6) then
            //return SUMMON_PHOENIX
        //elseif (mod == 0) then
        //    return SKILLED_DRIVER
        //endif
    endif

    return 'Aamk'
endfunction

function InitHero takes nothing returns nothing
	if (GetUnitCountDone(HERO_NEIL) > 0) then
		set hero_id = HERO_NEIL
    endif
endfunction

function UpdateWorkers takes nothing returns nothing
	loop
		call SetGoldPeons(5)
		call SetWoodPeons(0)
		call Sleep(1)
	endloop
endfunction

function ConfigureAI takes nothing returns nothing
    call SetTargetHeroes( true )
    call SetUnitsFlee( true )
    call SetHeroesFlee( true )
    call SetPeonsRepair( true )
    call SetHeroesBuyItems( true )
    call SetHeroesTakeItems( true )
    call SetSlowChopping( false )
	call SetWatchMegaTargets( true )
	call SetIgnoreInjured( false )
	call SetGroupsFlee( true )
	//call SetCaptainChanges ( true )
	call SetSmartArtillery ( true )
endfunction

function BuildingStrategy takes nothing returns nothing
    // **********************************
    // *      Building Strategy         *
    // **********************************
    // Tier 1 Buildings
    call SetReplacements( 1, 2, 3 )
    call SetBuildUnit( 1, TENET_CONTAINER )
    call SetBuildUnit( 6, TENET_PIONEER )
    call SetBuildUnit( 1, TENET_BARRACKS )
    call SetBuildUnit( 1, TENET_FACTORY )
	//call SetBuildUnit( 1, RUINED_BUILDING_1 )
	//call SetBuildUnit( 1, RUINED_BUILDING_2 )
	
	call SetBuildUpgr( 1, UPG_MEDIC )
	call SetBuildUpgr( 1, UPG_ENGINEER )
	call SetBuildUpgr( 1, UPG_MINES )
	call SetBuildUpgr( 1, UPG_ROCKET_LAUNCHER )
	call SetBuildUpgr( 1, UPG_STINGER )
	call SetBuildUpgr( 1, UPG_MINE_DETECTOR )
	call SetBuildUpgr( 3, UPG_IMPROVED_DEFENCE )
	
	call SetBuildUnit( 3, MACHINE_GUN_TURRET )
	call SetBuildUnit( 2, FLAMETHROWER_TURRET )
	call SetBuildUnit( 1, ANTI_AIR_TURRET )
	
    call CampaignDefenderEx( 2, 2, 3, TENET_SOLDIER )
    // **********************************
    // *    End Building Strategy       *
    // **********************************
endfunction

//===========================================================================
// Basic attack functionality
//===========================================================================
function AttackTarget takes unit target, boolean addAlliance returns nothing
    if (target == null) then
        return
    endif
    if (addAlliance) then
        call SetAllianceTarget( target )
    endif
    call FormGroup( 3, true )
    call AttackMoveKillA( target )
    if (not addAlliance) then
        call SetAllianceTarget( null )
    endif
endfunction

//===========================================================================
// Initiates an attack based on target priorities
//===========================================================================
function LaunchAttack takes nothing returns nothing
    local unit target = null
    local boolean setAlly = true

    // Don't launch any attack while town is threatened
    if (TownThreatened()) then
        call Sleep( 2 )
        return
    endif

    // Target Priority #1
    if (target == null) then
        set target = GetAllianceTarget()
        if (target != null) then
            set setAlly = false
        endif
    endif

    // Target Priority #2
    if (target == null) then
        set target = GetExpansionFoe()
        if (target != null) then
            set take_exp = false
        endif
    endif

    // Target Priority #3
    if (target == null) then
        set target = GetMegaTarget()
    endif

    // Target Priority #4
    if (target == null) then
        set target = GetEnemyExpansion()
    endif

    // Target Priority #5
    if (target == null) then
        set target = GetEnemyExpansion()
        if (target == null) then
            call StartGetEnemyBase(  )
            loop
                exitwhen (not WaitGetEnemyBase())
                call SuicideSleep( 1 )
            endloop
            set target = GetEnemyBase()
        endif
    endif

    // Target Priority #6
    if (target == null) then
        set target = GetCreepCamp( 0, 9, false )
    endif

    // Target Priority #7
    if (target == null) then
        set target = GetCreepCamp( 10, 100, true )
    endif

    // Attack the target and increment attack wave
    if (target != null) then
        call AttackTarget( target, setAlly )
    else
        // If no target was found, sleep a bit before trying again
        call Sleep( 20 )
    endif
endfunction

function AttackWaves takes nothing returns nothing
    loop //Init the infinite attack loop
        call InitAssaultGroup()
        call CampaignAttackerEx( 2, 3, 3, TENET_SOLDIER )
        call CampaignAttacker( 0, 1, hero_id )
        call Sleep( M3 ) // Waits 3 minutes before attacking
        call LaunchAttack()
    endloop
endfunction

function main takes nothing returns nothing
    call CampaignAI( null, function ChooseHeroSkill )
    call ConfigureAI( )
    call InitHero( )
	call StartThread(function UpdateWorkers)

    call BuildingStrategy( )

    call AttackWaves( )
endfunction